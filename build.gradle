buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.14.0'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.11'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.flywaydb.flyway' version '11.14.0'
    id "org.sonarqube" version '7.0.1.6134'
    id 'jacoco'
}

jacoco {
    toolVersion = "0.8.14"
}

dependencyManagement {
    imports {
        mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:3.1.1"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2024.0.0"
    }
}

def jacocoExcludes = [
        // --- Spring Boot/JPA 관련 ---
        '**/configuration/**',
        '**/config/**',
        '**/dto/**',
        '**/exception/**',
        '**/error/**',
        '**/filter/**',
        '**/handler/**',
        '**/security/**',
        '**/util/**',

        // --- QueryDSL 관련 ---
        '**/Q*.class',

        // --- 기타 생성 코드 ---
        '**/*Application.class',
        '**/Test*.class',

        // --- Flyway 관련 ---
        '**/db/migration/**',

        // --- Swagger 관련 ---
        '**/static/docs/**',
]

flyway {
    configFiles = ["$rootDir/conf/flyway.conf"]
    locations = ['filesystem:src/main/resources/db/migration']
    baselineOnMigrate = true
    baselineVersion = 1
    cleanDisabled = true
}

group = 'com.couponpop'
version = '0.0.1-SNAPSHOT'
description = 'couponpop-store-service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

// .env로부터 GITHUB_ACTOR와 GITHUB_TOKEN을 읽어오는 함수
def loadGithubCredentials() {
    def credentials = [:]
    def envFile = file('.env')
    def foundKeys = 0

    if (envFile.exists()) {
        envFile.eachLine { line ->
            if (foundKeys == 2) return // 두 키 다 찾으면 조기 종료
            if (!line.startsWith('#') && line.contains('=')) {
                def (key, value) = line.split('=', 2)*.trim()
                if (key in ['GITHUB_ACTOR', 'GITHUB_TOKEN']) {
                    if ((value.startsWith('"') && value.endsWith('"')) || (value.startsWith("'") && value.endsWith("'"))) {
                        value = value.substring(1, value.length() - 1)
                    }
                    credentials[key] = value
                    foundKeys++ //
                }
            }
        }
    }

    return credentials
}

repositories {
    mavenCentral()

    def githubCredentials = loadGithubCredentials()
    def githubUsername = System.getenv("GITHUB_ACTOR") ?: githubCredentials.GITHUB_ACTOR ?: "unknown"
    def githubToken = System.getenv("GITHUB_TOKEN") ?: githubCredentials.GITHUB_TOKEN ?: "unknown"

    maven {
        url = uri("https://maven.pkg.github.com/CouponPop/couponpop-security-module")
        credentials {
            username = githubUsername
            password = githubToken
        }
    }

    maven {
        url = uri("https://maven.pkg.github.com/CouponPop/couponpop-core-module")
        credentials {
            username = githubUsername
            password = githubToken
        }
    }
}

dependencies {
    // [Spring Boot Starter]
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // [CouponPop Module]
    implementation 'com.couponpop:couponpop-security:0.0.3-SNAPSHOT'
    implementation 'com.couponpop:couponpop-core:0.0.15-SNAPSHOT'

    // [Spring Cloud Starter]
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

    // [DB]
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'

    // [AWS]
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-parameter-store'

    // [Querydsl]
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // [Flyway]
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // [Elasticsearch]
    implementation 'co.elastic.clients:elasticsearch-java'

    // [Micrometer Prometheus Registry]
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // [Spatial/GIS]
    implementation 'org.hibernate.orm:hibernate-spatial:6.5.3.Final'
    implementation 'org.locationtech.jts:jts-core:1.19.0'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // [Compile & Runtime & Test]
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mysql'

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true // SonarQube용
        csv.required = false
        html.required = true // 개발자 확인용
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: jacocoExcludes)
                })
        )
    }
}